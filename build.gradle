buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:0.3.18'
        classpath 'com.netflix.nebula:gradle-info-plugin:7.1.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:17.0.5'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.17.3'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.6.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.2'
    }
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'

version System.env.CIRCLE_TAG ?: gitVersion()

allprojects {
    apply plugin: 'com.palantir.java-format'
    version rootProject.version
    group 'com.palantir.safethreadlocalrandom'

    repositories {
        jcenter()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.palantir.baseline-class-uniqueness'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror']
    }

    tasks.check.dependsOn(javadoc)
    sourceCompatibility = 1.8
}
